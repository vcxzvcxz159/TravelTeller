plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '3.5.8'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.C3'
version = '0.0.1-SNAPSHOT'
description = 'TravleTeller'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springAiVersion', "1.1.1")
    set('queryDslVersion', "5.0.0")
    set('ociSdkVersion', "3.74.1")
    set('jerseyVersion', "2.35")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.8"
        mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
        mavenBom "com.oracle.oci.sdk:oci-java-sdk-bom:${ociSdkVersion}"
        mavenBom "org.glassfish.jersey:jersey-bom:${jerseyVersion}"  // Jersey BOM 추가
    }
}

dependencies {
    // Spring Boot starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'jakarta.persistence:jakarta.persistence-api'

    // Spring AI
    implementation 'org.springframework.ai:spring-ai-starter-model-openai'

    // QueryDSL
    implementation "com.querydsl:querydsl-jpa:${queryDslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${queryDslVersion}:jakarta"

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Oracle JDBC & Security
    runtimeOnly 'com.oracle.database.jdbc:ojdbc11'
    implementation 'com.oracle.database.security:oraclepki:23.26.0.0.0'

    // OCI SDK
    implementation 'com.oracle.oci.sdk:oci-java-sdk-common'
    implementation 'com.oracle.oci.sdk:oci-java-sdk-core'
    implementation 'com.oracle.oci.sdk:oci-java-sdk-vault'
    implementation 'com.oracle.oci.sdk:oci-java-sdk-identity'
    implementation 'com.oracle.oci.sdk:oci-java-sdk-secrets'
    implementation "com.oracle.oci.sdk:oci-java-sdk-common-httpclient-jersey:${ociSdkVersion}"

    // Jersey & JAX-RS
    implementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    implementation 'org.glassfish.jersey.core:jersey-client'
    implementation 'org.glassfish.jersey.inject:jersey-hk2'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Springdoc OpenAPI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

    // Tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "build/generated/sources/annotationProcessor/java/main"]
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.annotationProcessorPath = configurations.annotationProcessor + configurations.compileClasspath
    options.generatedSourceOutputDirectory = file("build/generated/sources/annotationProcessor/java/main")
}

tasks.clean {
    delete file('build/generated/sources/annotationProcessor/java/main')
}

tasks.named('test') {
    useJUnitPlatform()
}
